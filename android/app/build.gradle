import gg.strims.ppspp.buildsrc.Libs

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.squareup.wire'
}

task findProto(type: Exec) {
    workingDir "../../"
    executable "find"
    args "schema", "-type", "f"
    standardOutput = new ByteArrayOutputStream()
    ext.files = {
        standardOutput.toString()
    }
    println standardOutput.toString()
}

task sanitizeProto(type: Exec) {
    workingDir "../../"
    commandLine "hack/remove-services.sh"
    ignoreExitValue true
    args "schema/sanitized/", findProto.files()
}

sanitizeProto.dependsOn findProto
preBuild.dependsOn sanitizeProto

wire {
    sourcePath {
        srcDir '../../schema/sanitized'
    }

    kotlin {
        out = 'src/main/java'
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "gg.strims.ppspp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName '0.0.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        allWarningsAsErrors = true
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion Libs.Kotlin.version
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }

}

//repositories {
//    flatDir {
//        dirs '.'
//    }
//}

dependencies {
    implementation name: 'bridge', ext: 'aar'

    implementation Libs.Proto.wire

    implementation Libs.Player.exoPlayer

    implementation Libs.Kotlin.stdlib
    implementation Libs.Coroutines.android

    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.uiKtx
    implementation Libs.material

    implementation Libs.AndroidX.Compose.layout
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.materialIconsExtended
    implementation Libs.AndroidX.Compose.tooling
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.runtimeLivedata

    testImplementation Libs.junit
    androidTestImplementation Libs.junit
    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.espressoCore
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.Ext.junit
    androidTestImplementation Libs.AndroidX.Compose.uiTest
}
